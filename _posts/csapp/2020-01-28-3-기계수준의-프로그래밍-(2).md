---
title:  "[CS:APP 3] 기계수준의 프로그래밍 (2)"
toc: true
tags: CS:APP
---

# Intro
프로그램에선 모든 instruction들이 직선적으로만 수행되지는 않는다. 조건에 따라 어느 구문으로 jump할 수도, 특정 구문을 반복할 수도 있다. 대부분의 고급 언어에선 for, if, switch 등이 이러한 역할을 수행하는데, Assembly에서는 이러한 구문들이 어떻게 구현 및 실행될까?


# Condition Code
프로세서는 실행되고 있는 프로그램에 대한 정보를 가지고 있어야 한다. 그 중 일부만 알아보자.

- Temporary data : %rax 등의 레지스터 값들
- runtime stack의 주소 : 이는 %rsp에 저장된다. (stack pointer)
- 현재 실행되는 코드의 위치 : 이는 %rip에 저장된다. (instruction pointer)
- 가장 최근 test에 대한 상태 : 바로 직전에 수행한 연산의 결과를 저장해놓는다. 이를 Condition code라 부르자.

Condition code는 가장 최근에 시행된 arithmetic / logical operation에 대한 결과를 저장한다. 별도로 세팅할 필요 없이 연산 후에 자동적으로 처리된다. (implicitly set) 단, lea instruction에서는 세팅되지 않는다.

여러 종류가 있지만, 가장 자주 쓰이는 것은 아래의 네 가지다.

- CF (Carry Flag) : MSB에서 carry-out이 일어난 경우. 그 특성 상 Unsigned overflow의 발생을 확인할 때도 사용할 수 있다.
- ZF (Zero Flag) : 연산 결과가 0이 된 경우. 또는 arithmetic operation에서의 equality를 확인할 때도 사용할 수 있다.
- SF (Sign Flag) : 연산 결과가 음수가 된 경우.
- OF (Overflow flag) : 연산 결과가 2's complement에서의 overflow를 발생시킨 경우. 즉, 양수 간 합에서 음수가 나온 경우(overflow)와 음수 간 합에서 양수가 나온 경우(underflow)를 모두 포함한다.

lea를 제외한 대부분의 산술 연산은 implicit하게 condition code를 세팅하지만, 직접적으로(explicitly) condition code를 세팅할 수 있는 instruction이 있다. 바로 cmp(q)와 test(q) instruction인데, 두 개의 operand를 받는 이 둘은 source가 둘이고 dest가 존재하지 않아, 연산 후 그 결과를 저장하지 않고, condition code만을 설정한다.

    cmpq Src2, Src1

Src1 - Src2 의 결과에 따라 condition code를 설정한다. 순서를 헷갈릴 수 있으니 주의하자. subq와 같은 순서라고 생각하면 편할 듯하다. 이는 CF, ZF, SF, OF 모두에게 영향을 끼칠 수 있다.

    testq Src2, Src1

Src1 & Src2 (bit 연산에서의 AND) 의 결과에 따라 condition code를 설정한다. 이는 ZF, SF에게만 영향을 끼칠 수 있다.

## Application
이제 condition code를 이용해, 조건에 따라 특정 명령을 수행하는 세 가지의 instruction에 대해 알아보자. 모든 instruction은 (Instruction)(suffix) 형태로, 그 suffix는 세 경우 모두 동일하다. 예를 들어, set instruction에 equal(e) 조건이라면 sete 와 같이 나타낸다. 아래 조건과 condition code가 어떻게 연결되는지 한 번씩 생각해보자.

suffix(X) | Condition Codes | Description
---|---|---
\- | 1 (True) | unconditional (무조건 실행)
e | ZF | equal or zero
ne | ~ZF | not equal or non-zero
s | SF | negative
ns | ~SF | non-negative
g | ~(SF^OF) & ~ZF | (signed) greater
ge | ~(SF^OF) | (signed) greater or equal
l | (SF^OF) | (signed) less
le | (SF^OF) \| ZF | (signed) less or equal
a | ~CF & ~ZF | (unsigned) above
b | CF | (unsigned) below

## `set` Instruction

    setX Dest

조건(X)에 따라 Dest에 해당하는 low-order byte 하나를 0 또는 1로 설정한다. 나머지 바이트들은 건드리지 않는 다는 점에서 주의해야 한다. 보통은 movz instruction을 통해 나머지 상위 비트들을 0으로 만들어준다. 예시를 통해 용례를 알아보자.

gt.c: (일부)

    int gt (long x, long y){
        return x > y;
    }

gt.s: (일부)

    cmpq %rsi, %rdi   # %rsi : y, %rdi : x
    setg %al          # x > y ?
    movzbl %al, %eax  # zero extension
    ret               # return

## `cmov` Instruction

    cmovX Src, Dest
    
조건(X)에 따른 mov instruction이다. Condition code를 검사한다는 점만 제외하면 mov instruction과 거의 동일하다. 자세한 설명은 생략하기로 한다.

## `jmp` Instruction

    jmpX Label
    
조건(X)에 따라 Label에 해당하는 코드 위치로의 jump 여부를 결정한다. C에서, if 후에 사용하는 goto 와 비슷하게 볼 수 있겠다. Label을 사용하지 않더라도, Dest에 \*operand를 사용해, indirect jump를 명령할 수 있다. 물론, operand엔 immediate가 올 수도, 레지스터가 올 수도, 메모리가 올 수도 있다.

추가로, jmp instruction이 어떤 방식으로 인코딩되는지도 알아보자. Linker 파트에서도 이와 연관된 내용이 나오기도 하고, 무엇보다도 꽤 흥미롭기 때문이다.

jmp instruction은 두 가지 방법으로 인코딩될 수 있다. 하나는 __PC-상대 주소__ 방법이고, 또 하나는 __절대 주소__ 방법이다. 일반적으로 전자가 많이 이용되는데, jump 대상이 되는 instruction의 주소와 jmp instruction _바로 다음_ 의 주소의 차이를 인코딩한다. 이는 PC(Program Counter)가 다음 instruction의 주소를 지정하기 때문인 것 같다. Textbook에 있는 아래 예시를 보자.

branch.c:

      movq  %rdi, %rax
      jmp   .L2
    .L3:
      sarq  %rax
    .L2:
      testq %rax, %rax
      jg    .L3
      rep; ret
      
branch.o: (역어셈블 코드 주석 포함)

    0: 48 89 f8         mov   %rdi, %rax
    3: eb 03            jmp   8<loop+0x8>
    5: 48 d1 f8         sar   %rax
    8: 48 85 c0         test  %rax, %rax
    b: 7f f8            jg    5<loop+0x5>
    d: f3 c3            repz  retq

두 번째 줄에서의 jmp는 시작점으로부터(loop) +0x8로 계산되어 있고, 다섯 번째 줄에서의 jmp는 시작점으로부터 +0x5로 계산되어 있는 데 반해, 왼쪽의 기계어 코드를 보면 각각 0x03, 0xf8(-8)로 그 목적지가 설정되어 있다.

첫 jmp instruction이 실행될 때, PC는 다음 instruction인 5를 가리키고 있다. 목적지는 8이므로, 0x03을 PC에 더해주면 의도했던 장소로 도달할 수 있다. 마찬가지로, 두 번째 jmp instruction이 실행될 때는 d를 가리키고 있다. 목적지는 5이므로, d + 0xf8 = 13 + (-8) = 5. 이렇게 PC-relative 방식을 사용함으로써 instruction을 간결하게 (위의 경우, 주소를 나타내기 위해 2 바이트만을 사용하였다.) 나타내었고, 비슷한 방식으로 여러 코드 파일이 연결되는 link에서도 원래의 적절한 위치를 잘 표현할 수 있다.


아무튼, 이를 활용해 고급 언어에서의 조건문, 반복문 등을 구현할 수 있다는 점에서, jmp instruction은 아주 유용하게 쓰인다. 예시로, 각종 제어문을 jmp instruction을 이용해 구현해보자.

아래는 if를 활용한, x-y의 절댓값을 리턴하는 함수다. x가 크면 x-y를 리턴하고, y가 크면 y-x를 리턴한다.

abs.o:

    abs:
       cmpq    %rsi, %rdi     # %rsi : argument y, %rdi : argument x
       jle     .L4            # if (x <= y) 에 대응
       movq    %rdi, %rax     # 아래는 else에 해당함. 즉 x > y인 경우
       subq    %rsi, %rax     
       ret                    # return x - y
    .L4:
       movq    %rsi, %rax
       subq    %rdi, %rax
       ret                    # return y - x
       
이젠 loop를 구현해보자. 아래는 argument x를 binary로 표현했을 때, 1이 몇 개인지 리턴하는 함수다. 반복적으로 한 칸씩 right shift 연산을 수행하며 1의 개수를 카운트한다.

popcount.o:

    popcount:
       movl    $0, %eax     # result = 0
    .L2:  
       movq    %rdi, %rdx
       andl    $1, %eax     # t = x & 0x1
       addq    %rdx, rax    # result += t (LSB가 0이었으면 0이, 1이었으면 1이 더해질 것이다.)
       shrq    %rdi         # x >>= 1
       jne     .L2          # x가 0이 아니면 .L2로 돌아가 다시 반복한다.
       rep; ret

그 외에도, for-loop, do-while, while-do 등 많은 구문을 구현할 수 있지만, 모두 textbook에 자세히 설명되어있으며, 대부분이 비슷한 flow를 따라가므로 생략한다.

마지막으로 switch statement를 jmp를 이용해 구현해보자. Switch문은 그 특성 상, jump를 수행할 블록도 많고 (많지 않았다면 switch가 아닌 다른 구문을 사용했을 것이다.) 이전보다 여러 경우에 대한 처리가 필요하다. 그 전에, 아래 C 코드 예제를 통해 switch문에 대해 가볍게 복습해보자.

switch_example.c:
      
    void switch_example(long x, long y, long z){
        long w = 1;
        switch (x) {    // switch는 정수 index(x)에 따른 다중 분기점을 만든다.
        case 1:         
            w = y*z;
            break;      // break를 통해 switch 블럭 밖으로 나간다.
        case 2:
            w = y/z;    // fall through case. break를 걸지 않으면 기본적으로 폴 스루가 발생한다.
                        // 바로 아래의 case를 조건과 관계없이 실행한다.
                        // 즉 case 2에선 w = y/z + z가 될 것이다.
        case 3:
            w += z;
            break;
        case 5:         // 이 또한 폴 스루의 일종이다. 
        case 6:         
            w -= z;
            break;
        default:        // 앞 조건이 모두 맞지 않을 때 실행된다.
            w = 2;
        }
        return w;
    }

물론, 위에서 배운 jmp를 단순히 이용하여 위의 C 코드를 구현할 수야 있겠지만, 그렇게 만들어진 코드는 깔끔하지 않고 복잡할 것이다. Assembly에서는 jump table을 제공하여 보다 효율적인 구현의 방법을 제공한다.

    .section    .rodata       # .rodata(read-only data) 목적코드 segment를 의미.
        .align 8              # address를 8(byte, 즉 quad word)의 배수 단위로 할당.   
    .L4:
        .quad   .L8           # case 0, default case
        .quad   .L3           # case 1
        .quad   .L5           # case 2
        .quad   .L9           # case 3
        .quad   .L8           # case 4
        .quad   .L7           # case 5
        .quad   .L7           # case 6
        
각 주소는 quad word의 간격으로 되어 있기 때문에, jmp 위치를 x에 대한 함수로 나타내어 정할 수 있을 것이다.

      switch_example:
          movq  %rdx, %rcx
          cmpq  $6, %rdi        
          ja    .L8             # x > 6이면 default case로 보냄.
          jmp   *.L4(,%rdi,8)   # jump table을 참조해, .L4 + x*8에 해당하는 주소로 jump한다.

전체를 다 구현하지는 않고, 마지막으로 fall through case 부분을 확인해보자.

    .L5:                        # case 2
        movq     %rsi, %rax   
        cqto                    # convert quad to octa, 64 bit 간 곱/몫은 128 bit(octa word)를 필요로 함.
        idivq    %rcx           # idiv는 %rax 값에 source를 나누어 %rax에 다시 저장한다. (y/z)
        jmp      .L6
    .L9:                        # case 3
        movl     $1, %eax       # w = 1
    .L6:
        addq     %rcx, %rax     # w += z
        ret
        
register | value
---|---
%rdi | x
%rsi | y
%rdx | z
%rax | return value


난이도도 그리 높지 않고, 포스팅하면서 코드도 조금 많이 쳐서 예제를 따로 풀어보진 않았다. 다음엔 프로시저(procedure)에 대해 알아보도록 하자.
