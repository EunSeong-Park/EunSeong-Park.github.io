---
title:  "[CS:APP 4] 기계수준의 프로그래밍 (3)"
toc: true
tags: CS:APP
---

# Intro
기계어 파트가 한참 남았는데 언제 끝날지 너무 막막하다. 그렇다고 다음 파트로 넘어가도 희망찰 것 같지는 않지만 아무튼 오늘은 프로시저에 대해 알아보자. Procedure는 어떠한 argument를 받아, 어떠한 값을 return하는 코드 블럭의 단위이자 abstraction이다. 이는 고급 언어에서 함수로, handler로, 혹은 subroutine으로 구현될 수 있다. 


# 프로시저의 Call과 Return
프로그래밍에서의 함수들이 그렇듯, 프로시저는 (자기 자신을 포함하여) 다른 프로시저를 호출할 수 있다. 호출된 프로시저(Q)는 그것에 해당하는 명령들을 수행하고 호출자(P)로 그 제어를 돌려놓을(return) 것이다. 이러한 동작들은 아래의 특징 중 하나 이상을 가질 수 있다. 이를 Procedure Calling Convention이라고 하자. 

1. Passing Control : PC(Program Counter)는 호출 시 Q의 starting address로 설정되며, return 시 Q를 call 하는 instruction 바로 다음의 주소로 설정된다.
2. Passing Data : P는 Q에게 하나 이상의 argument를 제공하고, Q는 P에게 하나의 return value를 제공한다.
3. Memory Management : Q는 시작 시 local variable 등을 위한 별도의 공간을 할당(alloacate)할 수 있고, return 시 이를 반납(deallocate)한다.

이 세 가지를, 뒤에서 예시를 통해 하나하나 알아볼 예정이다.

## Runtime Stack
스택(stack)은 프로시저 호출 동작에서의 메모리 관리 방식을 제공한다. 본격적으로 런타임 스택(Runtime Stack)에 대해 알아보기 전에, x86-64 스택 구조에 대해 간단히 알아보자.

![](/imgs/csapp/10.png)

x86-64 스택은 일반적인 스택을 뒤집어 놓은 형태로 볼 수 있겠다. 스택에 데이터를 누적할 수록 top의 주소는 점점 작은 값의 주소를 가지게 된다. 또, %rsp는 top element의 address를 저장하는 stack pointer로, 이 또한 함께 작은 값의 주소를 가지게 될 것이다.

    pushq   Src    # Src를 스택에 push
    popq    Dest   # 스택의 top element를 Dest에 저장한 뒤 pop, Dest는 반드시 레지스터여야한다.

이에 따라, push instruction은 %rsp의 decrement를 발생시키고, pop instruction은 %의 increment를 발생시킨다. 여기서는 quad word이므로, 주소는 8만큼 줄어들고, 늘어난다.

## x86-64 Stack Frame
스택을 프로시저의 argument를 전달하거나, return 정보를 저장하고, local variable의 저장 공간 등으로 활용할 수 있다. 그렇게 프로시저마다 하나의 프레임을 구성하는데, 이를 Stack Frame이라고 한다.

(필요하다면) 스택 프레임은 프로시저의 호출과 함께(즉, call instruction) 할당되며, 프로시저의 종료(즉, ret instruction)와 함께 반납된다. 호출이 여러 계층으로 일어날 경우, 프레임은 호출된 순서로 push되고, 반납된 순서로 pop될 것이다. 

스택 프레임의 구조를 대강 확인해보자.

![](/imgs/csapp/11.png)

- Return Address : 피호출자 리턴 시 돌아갈 주소를 저장한다.
- Frame Pointer : 호출자의 프레임과 피호출자의 프레임이 구분되는 주소.
- Argument Build Area : 피호출자가 또 다른 프로시저를 호출하려 할 때, argument가 레지스터에 다 저장하지 못할 정도로 그 수가 많다면 해당 영역에 argument를 저장한다.

현재 실행되는 프로시저는 top에 위치한다. 프로시저는 저 구조 중 필요한 것들만을 할당해 사용한다. 예시로, argument가 여섯 개 이하라면 굳이 스택에 넣을 필요 없이 argument를 레지스터(%rsi 등)에 저장한다. 심지어, 스택 프레임 자체를 필요로 하지 않는 함수들도 있다. 대부분의 간단한 프로시저라면 모든 local variable이나 argument를 레지스터에 보관할 수 있고, 다른 프로시저를 호출하지도 않을 것이니 말이다.


# Procedure Calling Convention
## Passing Control (제어 이동)
프로시저 P가 Q를 호출함은 제어(control)가 P에서 Q로 전달됨을 의미한다. 또, 이는 PC가 P에서 Q의 시작점으로 이동함을 의미한다. 마찬가지로, 프로시저 Q가 종료되어 return이 일어난 경우, 제어는 Q에서 P로 되돌아가고, PC가 P의 호출 instruction바로 다음으로 이동한다. 이 때, return을 통해 원래 P의 장소로 돌아가기 위해, 프로세서는 돌아가야 하는 코드의 위치를 기억하고 있어야 한다. 즉, call instruction은 PC를 Q의 시작으로 넘기는 것에 더하여, (P에서의) call instruction 직후의 코드 주소 push 또한 수행해야 한다.

    0000000000400540 <multstore>:
      ...
      ...
      400544: callq   400550 <mult2>    # [1] %rip = 0x400550, %rsp 8 감소, 0x400549 push
      400549: mov     %rax, (%rbx)      # [4]
      ...
      ...
    
    0000000000400550 <mult2>:
      400550: mov     %rdi, %rax        # [2] %rip = 0x400557
      ...
      ...
      400557: retq                      # [3] 0x400549 pop 이후 %rip에 저장, %rsp 8 증가.
      
## Passing Data (데이터 전송)
프로시저의 return은 제어의 전달뿐 아니라 어떠한 값을 호출자에게 제공하는 역할을 수행하기도 한다. 대부분 언어서의 함수의 return value가 그것이다. 이미 앞에서 본 예시를 통해 %rsi, %rdi 등으로 argument를 받아 프로시저 내의 명령들을 수행한 뒤 %rax로 값을 리턴하는 걸 확인했다. 

Argument를 레지스터로 받는 건 최대 6개까지다. 그 이상은 스택 프레임의 argument build area 영역에 저장된다. 아래 표의 순서와 크기대로 argument를 받는다.

size | #1 | #2 | #3 | #4 | #5 | #6
---|---|---|---|---|---|---
64 | %rdi | %rsi | %rdx | %rcx | %r8 | %r9
32 | %edi | %esi | %edx | %ecx | %r8d | %r9d
16 | %di | %si | %dx | %cx | %r8w | %r9w
8 | %dil | %sil | %dl | %cl | %r8b | %r9b

다만 일반적으로 7개 이상의 argument를 받는 프로시저는 드물어 스택에까지 저장하는 일은 그리 많지는 않(은 것 같)다. 여기는 이미 많이 경험한 부분이라 많이 설명할 필요 없을 듯.


## Managing Local Data (로컬 데이터 관리)
로컬 데이터를 저장할 방법이 여럿 있다. 레지스터에 저장해놓고 쓸 수도 있고, 스택에 쌓아놓고 쓸 수도 있다. 상황이나 필요에 따라 그 경우가 달라질텐데, 각각의 경우를 예시를 통해 알아보고, 그 과정 속에서의 convention들에 대해 알아보자.

### Stack Saving
기본적으로 컴파일러는 local data를 스택과 같은 메모리에 저장하려 하지 않는다. 즉, 필요한 경우에만 stack에 저장한다는 의미다.

- 레지스터의 수가 부족한 경우
- &x와 같이 local data의 주소를 필요로 하는 경우
- local data가 array나 struct인 

스택을 local data의 저장소로 사용하기 위해, 시작 시 stack pointer, %rsp에 sub instruction을 통해 미리 공간을 할당하며, 프로시저가 종료될 때, 혹은 데이터 사용이 끝났을 때는 add instruction을 통해 할당한 공간을 반납한다.

아래의 proc 함수는 argument를 총 여덟 개 받는다. a4, \*a4p는 proc의 호출자가 스택의 0~7, 8~15에 저장했을 것이다. 데이터 길이는 반드시 8의 배수임을 잊지 않도록 하자.

proc.c:

    void proc(long  a1, long  *a1p,
              int   a2, int   *a2p,
              short a3, short *a3p,
              char  a4, char  *a4p) {
        *a1p += a1;
        *a2p += a2;
        *a3p += a3;
        *a4p += a4;
    }

proc.o:

    proc:
    .LFB0:
        .cfi_startproc
        movq    16(%rsp), %rax    # 15~8까지 a4p Fetch
        addq    %rdi, (%rsi)      # *a1p += a1
        addl    %edx, (%rcx)      # *a2p += a2
        addw    %r8w, (%r9)       # *a3p += a3
        movl    8(%rsp), %edx     # 0~7까지 a4 Fetch
        addb    %dl, (%rax)       # *a4p += a4
        ret                       # Return
        .cfi_endproc

argument a4, \*a4p는 스택을 통해 값을 받았음을 확인할 수 있다. -16에서 a4p를 가져왔고, -8에서 a4를 가져왔다.

이제, 이 함수를 호출하는 함수, call_proc을 만들어보자. 많은 argument와 많은 local variable을 통해 스택 프레임이 어떻게 이용되는지 알아보고자 한다.

call_proc.c:

    long call_proc()
    {
        long x1 = 1;   int x2 = 2;
        short x3 = 3;  char x4 = 4;
        proc(x1, &x1, x2, &x2, x3, &x3, x4, &x4);
        return (x1+x2)*(x3-x4);
    }

call_proc.o:

    call_proc:
        # local variable을 스택에 저장하는 과정
        subq   $32, %rsp        # 32 byte의 스택 프레임 할당
        movq   $1, 24(%rsp)     # 31~24 까지 &x1 저장
        movl   $2, 20(%rsp)     # 23~20 까지 &x2 저장 
        movw   $3, 18(%rsp)     # 19~18 까지 &x3 저장
        movb   $4, 17(%rsp)     # 17에 &x4 저장

        # argument 저장
        leaq   17(%rsp), %rax   
        movq   %rax, 8(%rsp)    # 15~8 : argument 8, &x4 저장
        movl   $4, (%rsp)       # 7~0  : argument 7, x4(=4) 저장
        leaq   18(%rsp), %r9    # argument 6, leaq를 통해 해당 주소를 %r9에 저장함  
        movl   $3, %r8d         # argument 5
        leaq   20(%rsp), %rcx   # argument 4
        movl   $2, %edx         # argument 3
        leaq   24(%rsp), %rsi   # argument 2
        movl   $1, %edi         # argument 1

        # 함수 호출
        call   proc

        # call_proc의 return value 계산
        movslq 20(%rsp), %rdx   # x2를 long으로 convert (아래에서 x1+x2를 계산하기 위해)
        addq   24(%rsp), %rdx   # x1+x2
        movswl 18(%rsp), %eax   # x3을 int로 convert
        movsbl 17(%rsp), %ecx   # x4를 int로 convert
        subl   %ecx, %eax       # x3-x4
        ctlq                    # long으로 확장, imul을 위해 비트가 더 필요하므로.
        imulq  %rdx, %rax       # (x1+x2) * (x3-x4)

        # 메모리 복구 및 리턴
        addq   $32, %rsp        # 스택 프레임의 deallocation.
        ret                     # 리턴.
    

### Register Saving    
레지스터를 local data의 storage로 사용하는 경우는 stack의 때보다 조금 더 미묘하다. 레지스터는 그 수가 한정적이며, 모든 프로시저가 한 그룹의 레지스터를 공유해야하므로 overwriting의 위험이 있기 때문이다. 예를 들어, 호출자 P가 %rdx를 local variable로 쓰다가 피호출자 Q를 호출했다고 하자. 그런데, Q에서도 local variable로 %rdx를 사용했다면? P에서의 local variable은 Q에 의해 의도하지 않은 값이 될 수도 있다. 

이러한 위험을 방지하기 위해 정해진 convention이 있다(여기선 x86-64를 기준으로 한다). 레지스터의 종류에 따라 호출자(caller) 혹은 피호출자(callee) 중 하나가 원래의 값을 저장하는 규칙이다.

#### Caller-saved register
호출자가 호출 전에 local data를 frame에 별도로 저장해야 한다. return value인 %rax, %rdi ... %r9 등의 argument, 그리고 %r10, %r11은 caller-saved register다. 모두 피호출자에 의해 자유로이 변경될 수 있기 때문에 호출자는 이를 미리 백업해야 한다.

#### Callee-saved register
피호출자는 해당 레지스터의 값을 아예 변경하지 않거나, 원래의 값을 stack에 push한 다음, 리턴 직전에 pop해서 값을 복구해야 한다. %rbx, %r12 ... %r14, %rbp, 그리고 %rsp는 callee-saved register다. 우리가 로컬 데이터를 다루기 위해 stack pointer, %rsp를 사용한 방식을 생각하면 이해하기 좋을 것 같다.

다시 정리하면,

Caller-saved | Callee-saved
---|---
%rax, %rdi, %rsi, %rdx, %rcx, %r8, %r9, %r10, %r11 | %rbx, %r12, %r13, %r14, %rbp, %rsp

    
# Recursion (재귀)
프로시저 마지막 파트다. 프로시저와 관련이 있긴 하지만, 어쩐지 바로 앞 내용들로부터 붕 뜨는 느낌이 없지 않다. 분량이 짧고 익숙한 개념이니 빠르게 짚고 넘어가자.

알다시피 recursion은 프로시저가 스스로를 호출하는 것을 의미한다. 사실 스스로를 호출한다는 점만 제외하면 일반적인 프로시저의 호출과 별로 다를 게 없다. 따라서 마찬가지로 각 호출마다 새로운 스택 프레임을 할당하고 리턴시 반환하는 (recursion이 공간복잡도를 증가시키는 원인이겠다) 일반적인 과정을 거친다. 다만 뒤쪽의 프로시저가 앞쪽의 local data를 훼손하지 않도록, 그리고 recursion이 base case에 다가갈 수 있도록 주의해야 할 것이다. 

예시로, 이전 포스팅에서도 했었던, 주어진 input의 binary 표현에서 1의 개수를 찾는 pop count를 만들어보자.

pcount_r.c:

    long pcount_r(unsigned long x){
        if (x == 0)
            return 0;
        else
            return (x % 1) + pcount_r (x >> 1);
    }

pcount_r.o:

    pcount_r:
      movl     $0, %eax
      testq    %rdi, %rdi  # non-zero test를 일반적으로 이렇게 한다.
      je       .L6         # base case.
      pushq    %rbx        # 아래에서 %rbx를 사용하기 전에 백업 (callee-saved이므로)
      movq     %rdi, %rbx  # %rbx에 argument x 저장
      andl     $1, %ebx    # x & 1
      shrq     %rdi        # x >> 1, 그리고 이 값은 다음 call의 argument가 된다.
      call     pcount_r
      addq     %rbx, %rax  # 다음 프로시저로부터 받은 %rax에 현재 프로시저의 and 연산 결과를 더함
      popq     %rbx        # 이전 프로시저의 %rbx값 복구
    .L6:
      rep; ret             # 리턴
      

다음엔 array와 struct 같은 heterogeneous data structure가 Assembly에서 어떻게 구현되는지 알아보자. Chapter 3이 이렇게 분량이 많을 줄은 몰랐다.. 
