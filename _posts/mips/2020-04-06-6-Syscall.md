---
title:  "[MIPS 6] Syscall"
tags: MIPS
toc: true
---

# Intro
EduMIPS64에서 사용되는 Syscall을 알아보자. 


# Syscall
`syscall` 명령어는 우리가 OS-like 인터페이스를 이용할 수 있도록 하는 명령어다. 공통적으로, t6(14번) 레지스터를 값을 파라미터의 주소로 여기며, at(1번) 레지스터에 자신의 리턴 값을 삽입한다. 단, 그 종류에 따라 파라미터를 따질 수도 있고, 그렇지 않을 수도 있다. 리턴 값도 마찬가지다.

## SYSCALL 0: exit()
어느 파라미터도 받지 않으며, 어느 리턴 값도 내놓지 않고 시뮬레이터를 종료한다.

## SYSCALL 1: open()
`syscall 1`은 두 개의 파라미터를 받는다. 하나는 오픈할 파일의 null-terminated string으로 구성된 경로, 또 하나는 어떻게 파일을 오픈할지를 지정하는 플래그를 담은 더블 워드다. 더블 워드의 값은 설정할 각각의 플래그가 가지는 값을 모두 더하면 된다.

Flag(integer) | Description
---|---
O_RDONLY (0x01) | 읽기 전용 모드로 연다.
O_WRONLY (0x02) | 쓰기 전용 모드로 연다.
O_RDWR (0x03) | 읽기&쓰기 모드로 연다.
O_CREAT (0x04) | 파일이 존재하지 않는다면 새로 만든다.
O_APPEND (0x08) | 쓰기 모드에서, 파일 끝에 텍스트를 추가한다.
O_TRUNC (0x08) | 쓰기 모드에서, 파일이 열리면 파일의 컨텐츠를 지운다.

예를 들어, 쓰기 모드로 append를 지정하고 싶으면 2+8=10을 파라미터에 넣어주면 된다. 여기서, 읽기 / 쓰기 / 읽기&쓰기 모드는 반드시 지정해야 한다. 저렇게 값이 지정되면 값이 유일하게 모드를 지정하지 못하지 않나 싶지만, 일부는 배타적(exclusive)이기 때문에 문제 없다.

`syscall 1`은 리턴 값으로 연 파일의 파일 식별자(file descriptor)를 내놓는다. 이는 다른 `syscall`을 위해 사용될 수 있으며, 만약 오류가 발생하면 -1을 리턴한다.

예시를 보자. 어디서 주워왔다.

                    .data
    error_op:       .asciiz     "Error opening the file"
    ok_message:     .asciiz     "All right"
    
                    ;; 파라미터 두 개
    params_sys1:    .asciiz     "filename.txt"
                    .word64     0xF
                    
                    .text
                    ;; params_sys1의 주소를 r14에 넣고 오픈한다.
    open:           daddi       r14, r0, params_sys1
                    syscall     1

                    ;; -1(에러) / 파일 식별자와의 비교를 위함
                    daddi       $s0, r0, -1
                    dadd        $s2, r0, r1
                    
                    ;; 리턴 값(r1)에 따라 print_string의 파라미터를 결정
                    daddi       $a0,r0,ok_message
                    bne         r1,$s0,end
                    daddi       $a0,r0,error_op

    end:            jal         print_string
                    syscall     0
                    
                    #include    print.s

## SYSCALL 2: close()
`syscall 2`는 닫을 파일의 파일 식별자를 파라미터로 받는다. 성공적으로 닫았으면 0, 실패하면 -1을 리턴한다. 

## SYSCALL 3: read()
`syscall 3`은 읽을 파일에 대한 식별자, 읽은 데이터를 저장할 주소, 읽을 데이터의 크기(byte), 총 세 개의 파라미터를 받는다.

파일 식별자가 0(standard input)이라면, 시뮬레이터는 프롬프트를 띄워 사용자에게 데이터를 받는다. 이 때, 인풋의 크기가 지정한 것보다 크다면, 시뮬레이터는 메세지를 다시 띄운다.

성공적으로 데이터를 받은 경우 읽은 데이터의 바이트 수를, 아니면 -1을 리턴한다. 

## SYSCALL 4: write()
`syscall 4`는 쓸 파일에 대한 식별자, 읽을 데이터의 주소, 쓸 데이터의 크기(byte), 총 세 개의 파라미터를 받는다. 아래 예시를 보자. 물론 이것도 어디서 주워왔다.

                    .data
                    ;; 세 개의 파라미터
    params_sys4:    .space      8
    ind_value:      .space      8
                    .word64     16
                    ;; 에러 / 성공 시 메세지
    error_4:        .asciiz     "Error writing to file"
    ok_message:     .asciiz     "All right"

    value:          .space      30

                    .text
                    ;; 파라미터 주소를 r14에 삽입.
    write:          daddi       r14, r0,params_sys4
                    ;; s2에 식별자가 있었다고 가정하고 파라미터 입력
                    sw          $s2, params_sys4(r0)
                    ;; value 주소를 s1에 저장, 이후 쓸 위치로 지정.
                    daddi       $s1, r0, value
                    sw          $s1, ind_value(r0)
                    syscall     4

                    ;; 리턴값으로 에러 여부 확인
                    daddi       $s0, r0,-1
                    daddi       $a0, r0,ok_message
                    bne         r1, $s0,end
                    daddi       $a0, r0,error_4

    end:            jal         print_string
                    syscall     0

                    #include    print.s

아, 예시 날로 먹었다!

## SYSCALL 5: printf()
`syscall 5`는 받을 파라미터의 수가 가변적이다. 우선, 첫 파라미터는 format string의 주소다.(우리가 아는 그거 맞다) 포맷 스트링은 다수의 placeholder를 포함할 수 있는데, 들어갈 데이터의 종류에 따라 다음과 같이 사용할 수 있다.

Placeholder | Description
---|---
%i, %d | Integer
%s | String
%% | Literal %

`syscall 5`는 각각의 placeholder로부터 파라미터를 찾는다. 쓰여진 바이트 수가 r1에 저장되며, 오류 발생 시 -1이 저장된다.

이번에도 예시를 날로 먹어보자.

                    .data
                    ;; 포맷 스트링 with 5 placeholder
    format_str:     .asciiz   "%dth of %s:\n%s version %i.%i is being tested!"
    s1:             .asciiz   "June"
    s2:             .asciiz   "EduMIPS64"
                    ;; 파라미터
    fs_addr:        .space    4
                    .word     5
    s1_addr:        .space    4
    s2_addr:        .space    4
                    .word     0
                    .word     5
    test:
                    .code
                    ;; 포맷 스트링 주소를 r5에 저장하고 첫 파라미터에 삽입
                    daddi     r5, r0, format_str
                    sw        r5, fs_addr(r0)
                    
                    ;; string placeholder를 각각 파라미터에 저장
                    daddi     r2, r0, s1
                    daddi     r3, r0, s2
                    sd        r2, s1_addr(r0)
                    sd        r3, s2_addr(r0)
                    ;; 파라미터가 올 주소를 지정한 후 호출. 
                    daddi     r14, r0, fs_addr
                    syscall   5
                    syscall   0

이를 실행하면

    5th of June:
    EduMIPS64 version 0.5 is being tested!

위와 같은 결과가 나온다. Placeholder 순서대로 파라미터를 넣어주자.
